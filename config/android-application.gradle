apply from: "../config/version-update.gradle"

android {

    buildToolsVersion versions.android.buildTools
    compileSdkVersion versions.android.compileSdk

    buildTypes {

        debug {
            debuggable true
            testCoverageEnabled false
        }

        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')
            proguardFiles 'proguard-android.pro'
        }
    }

    flavorDimensions "default"
    productFlavors {
        // Used to run UI tests mocking the domain part
        mock {
        }
        // Normal flavor
        normal {
        }
    }

    compileOptions {
        encoding "UTF-8"
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId 'com.marcohc.terminator.sample'
        targetSdkVersion versions.android.targetSdk
        minSdkVersion versions.android.minSdk
        versionCode getVersionCodeTimestamp()
        versionName "3.0.0"
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner "com.marcohc.terminator.core.mvi.android.test.RxTestRunner"
    }

    testOptions {
        animationsDisabled = true
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }

    // To remove jetbrains annotations dex error
    configurations {
        cleanedAnnotations
        compile.exclude group: 'org.jetbrains', module: 'annotations'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }
}

static def getVersionCodeTimestamp() {
    def date = new Date()
    def formattedDate = date.format('yyMMddHHmm')
    def code = formattedDate.toInteger()
    return code
}
